import glob

Import('*')

env.Append(CPPPATH = ['#/src'])

subdirs = ['']

# Source
src = ['client.cpp']
for dir in subdirs:
    src += Glob('fah/client/' + dir + '/*.cpp')

if env['wrap_glibc']: src.append('fah/client/glibc.c')

# Resources
res = env.Resources('resources.cpp', ['#/src/resources'])
resLib = env.Library('fahclient-resources', res)
Precious(resLib)
src += resLib

if env['PLATFORM'] == 'win32' or int(env.get('cross_mingw', 0)):
    src += [env.RES('fah/client/win/SysTray.rc')]
    src += Glob('fah/client/win/*.cpp')

elif env['PLATFORM'] == 'darwin': src += Glob('fah/client/osx/*.cpp')
else: src += Glob('fah/client/lin/*.cpp')

# Build Info
info = env.BuildInfo('build_info.cpp', [])
AlwaysBuild(info)
src += [info]

# FAHClient
client = env.Program('#/fah-client', src)

Return('client')
